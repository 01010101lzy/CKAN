#!/usr/bin/perl
use 5.010;
use strict;
use warnings;
use autodie;

use JSON::Any;
use File::Slurp;
use Data::Dumper;
use Archive::Zip;
use Getopt::Std;
use WWW::Mechanize;
use File::Temp;

# Reference implementation of the Comprehensive Kerbal Archive Network (CKAN)
# installer.
#
# Paul '@pjf' Fenwick, September 2014
#
# This code may be used under the following licenses:
#   - MIT
#   - CC-BY
#   - Same as Perl 5 itself

my $j    = JSON::Any->new;
my $mech = WWW::Mechanize->new;

# Option handling...

my %opts = (
    i => 0,     # Install
    v => 0,     # Verbose
    f => '',    # File (if available locally)
    g => "$ENV{HOME}/Steam/SteamApps/common/Kerbal Space Program", # Game
);

getopts('vig:f:', \%opts);

my $GAME     = $opts{g};
my $GAMEDATA = File::Spec->catdir( $GAME, 'GameData' );

# Our most basic operation is to read files from the commandline and install
# their contents.

my (@files) = @ARGV;

foreach my $filename (@files) {
    my $contents = read_file( $filename );
    my $meta = $j->decode($contents);

    if ($opts{i}) {

        my $filename = $opts{f};

        # We need our tmpfile defined in this outer scope so
        # it doesn't try to prematurely clean away the tmpfile
        # when it goes out of scope in the inner scope.

        my $tmpfile = File::Temp->new();

        if (not $filename) {
            my $download = $meta->{download};

            say "Fetching $download...";

            $mech->get( $download );

            # It would be lovely if we could pass mech a filehandle here,
            # as right now there's a potential race condition if another
            # process (re)moves $zipfile before mech opens it.

            $filename   = $tmpfile->filename;
            $mech->save_content( $filename );
        }

        say "Inspecting contents...";

        my $zip = Archive::Zip->new;
        $zip->read( $filename );

        if ($opts{v}) {
            foreach my $file ($zip->memberNames) {
                say $file;
            }
        }

        say "Installing...";

        foreach my $install ( @{ $meta->{install} }, @{ $meta->{bundles} } ) {
            my $file       = $install->{file};
            my $install_to = $install->{install_to};

            say "$file...";

            if ($install_to eq 'GameData') {
                $zip->extractTree( $file, File::Spec->catdir($GAMEDATA, $file) );
            }
            else {
                die "Don't know how to install to $install_to";
            }
        }
    }
}
