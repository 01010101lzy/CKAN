#!/usr/bin/perl
use 5.010;
use strict;
use warnings;
use autodie qw(:all);
use JSON::PP qw(decode_json);
use FindBin qw($Bin);
use File::Copy qw(move);

use constant INDEX    => "$Bin/../KSMods.json";
use constant KS2CKAN  => "$Bin/../ks2ckan.exe";
use constant CKANMETA => "$Bin/../../CKAN-meta/";

# Generate everything we need for the KerbalStuffPAN (ks2ckan)

# Hop over to our project root.
chdir("$Bin/..");

my $index = decode_json( slurp(INDEX) );

my @modlist;

# Process mods listed on the commandline, or the whole modlist by default.
if (@ARGV) {
    @modlist = @ARGV;
}
else {
    @modlist = keys %{ $index->{modlist} };
}

foreach my $mod (@modlist) {
    say "$mod...";

    $index->{modlist}{$mod} or die "Cannot find data for $mod\n";

    # Convert our mod
    system(KS2CKAN, "ks", $mod, $index->{modlist}{$mod}, CKANMETA);

    # TODO: Automatically submit changes to git.
}

sub slurp {
    my ($filename) = @_;

    open(my $fh, '<', $filename);

    local $/;   # Slurp mode!

    return <$fh>;
}
